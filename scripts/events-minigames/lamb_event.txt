//===== rAthena Script ======================================= 
//= Lamb Hunting Event
//===== By: ==================================================
//= KiNa
//===== Description: =========================================
//= YggdrasilRO's Lamb Event
//= A number of lambs will appear when the event start
//= Lambs drop valuable rewards
//= Killing the last lamb will give an additional reward.
//===== Current Version: =====================================
//= 1.0
//============================================================

-	script	Peaceful_Lamb	-1,{
OnClock0000:
OnClock0200:
OnClock0400:
OnClock0600:
OnClock0800:
OnClock1000:
OnClock1200:
OnClock1400:
OnClock1600:
OnClock1800:
OnClock2000:
OnClock2200:
    sleep 10000;
    killmonster "aimia",strnpcinfo(3)+"::OnLambDead";
    donpcevent strnpcinfo(3)+"::OnLambSummon";
end;

OnLambSummon:
    .mobid = 31985;
    .mobs = 20;
    announce "لقد ظهر عدد من الخراف في مدينة ايميا, ابحثو عنهم حالاً", bc_all|bc_blue;
    monster "aimia",0,0,"--ja--",.mobid,.mobs,strnpcinfo(3)+"::OnLambDead";
end;

On100LambSummon:
    .mobid = 31985;
    .mobs = 100;
    announce "لقد ظهر عدد من الخراف في مدينة ايميا, ابحثو عنهم حالاً", bc_all|bc_blue;
    monster "aimia",0,0,"--ja--",.mobid,.mobs,strnpcinfo(3)+"::OnLambDead";
end;

OnLambDead:
    .mobs--;
        if(.mobs == 0){
            announce callfunc("formatArabic"," لقد قام اللاعب "+strcharinfo(0)+" بالقبض على آخر خروف وحصل على 30 تذكرة إضافية "), bc_all|bc_blue;
            getitem 50007,30,getcharid(3);
            donpcevent strnpcinfo(3)+"::OnLambEventEnd"; end;
        }
        else {
            specialeffect2 313;
            announce callfunc("formatArabic"," لقد قام اللاعب "+strcharinfo(0)+" بالقبض على أحد الخراف, تبقى "+.mobs+" خروف"), bc_all|bc_blue;
        }
end;

OnLambEventEnd:
    announce "لقد تم القبض على جميع الخراف في مدينة إيميا.",bc_all|bc_blue;
end;

OnInit:
    bindatcmd("spinlamb",strnpcinfo(3)+"::OnLambSummon",80,80);
    bindatcmd("spinlamb100",strnpcinfo(3)+"::On100LambSummon",80,80);
end;
}